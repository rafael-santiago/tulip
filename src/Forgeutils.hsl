#
#                           Copyright (C) 2005-2016 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
include ~/fsutil.hsl
include on linux forgesupp/linux/pack_and_go_funcs.hsl
include on freebsd forgesupp/freebsd/pack_and_go_funcs.hsl
include on windows forgesupp/windows/pack_and_go_funcs.hsl

function help() : result type none {
    var help type list;
    $help = hefesto.sys.get_option("help");
    if ($help.count() > 0) {
        hefesto.sys.echo("--help -> You got the main info about these options\n" +
                         "--devels-forge -> It runs a faster build. Desired while developing some stuff or maintaining\n" +
                         "--pack-and-go -> It generates a prebuilt for the current platform under prebuilt sub-directory and updates README.md\n" +
                         "--test-options ->  Concentrates specific options to be passed to the testing build\n" + 
                         "--sub-modules ->  Concentrates the relevant directories containing the Tulip's parts\n" +
                         "--includes -> Specifies the additional include's path\n" + 
                         "--install -> Installs Tulip\n" +
                         "--uninstall -> Uninstalls Tulip\n");
        hefesto.project.abort(0);
    }
}

function set_prebuilt() : result type int {

    if (can_perform_pack_and_go() == 0) {
        result 0;
    }

    var oldcwd type string;

    $oldcwd = hefesto.sys.pwd();

    if (hefesto.sys.cd("../prebuilt") == 0) {
        hefesto.sys.mkdir("../prebuilt");
    } else {
        hefesto.sys.cd($oldcwd);
    }

    var src_bin type string;
    var dst_bin type string;

    $src_bin = "../bin/tulip";
    $dst_bin = "../prebuilt/tulip-bin-" + hefesto.sys.os_name() + ".zip";

    if (hefesto.sys.os_name() == "windows") {
        $src_bin = $src_bin + ".exe";
    }

    if (isfile($src_bin) == 0) {
        hefesto.sys.echo("PACK AND GO WARN: The src " + $src_bin + " is unaccessible.\n");
        result 0;
    }

    if (pack_stuff($src_bin, $dst_bin) != 0) {
        hefesto.sys.echo("PACK AND GO WARN: Unable to zip the file " + $src_bin + " into " + $dst_bin + "\n");
        result 0;
    }

    result refresh_prebuilt_md5sum($dst_bin);
}

local function refresh_prebuilt_md5sum(dst_bin_path type string) : result type int {
    var exit_code type int;

    if (isfile("../README.md~")) {
        var option type string;

        $option = "?";
        while ($option != "y" && $option != "n" && $option != "c") {
            $option = hefesto.sys.prompt("PACK AND GO QUESTION: Human, it seems that you have already tried to run it and maybe it did not work... Maybe would be a good idea to restore the ../README.md from ../README.md~ before proceeding. Do you want to restore it? [y/n/c] ");
        }

        hefesto.sys.echo("\n");

        if ($option == "c") {
            hefesto.sys.echo("PACK AND GO WARN: Okay, we will abort from here.\n");
            result 0;
        }

        if ($option == "y") {
            if (hefesto.sys.cp("../README.md~", "../README.md") == 0) {
                hefesto.sys.echo("PACK AND GO WARN: Error while trying to copy ../README.md~ to ../README.md.\n");
                result 0;
            }
        }

        hefesto.sys.rm("../README.md~");
    }

    var md5hash type string;

    if (eval_md5_file_hash($dst_bin_path, $md5hash) != 0) {
        hefesto.sys.echo("PACK AND GO WARN: Error during md5 hash evaluation.\n");
        result 0;
    }

    var search_pattern type string;

    $search_pattern = "| ``prebuilt/tulip-bin-" + hefesto.sys.os_name() + ".zip`` | ``" + hefesto.sys.os_name() + "`` |.*|";

    var replacement_data type string;

    $replacement_data = "| ``prebuilt/tulip-bin-" + hefesto.sys.os_name() + ".zip`` | ``" + hefesto.sys.os_name() +  "`` | ``" + $md5hash + "`` |";

    if (hefesto.sys.cp("../README.md", "../README.md~")) {
        var replacement_count type int;
        $replacement_count = hefesto.sys.replace_in_file("../README.md", $search_pattern, $replacement_data);
        if ($replacement_count != 1) {
            hefesto.sys.echo("PACK AND GO WARN: Something seems to be gone wrong during the README.md updating. It is better you get a diff of it after committing/pushing.\n");
            result 0;
        }
    }

    result 1;
}
