#
#                           Copyright (C) 2005-2016 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
include ../Toolsets.hsl
include Forgeutils.hsl

var src type list;
var inc type list;
var cflags type list;
var lib type list;
var ldflags type list;
var appname type string;
var curr_toolset type string;

project tulip-tests : toolset $curr_toolset : $src, $inc, $cflags, $lib, $ldflags, $appname ;

tulip-tests.preloading() {
    $curr_toolset = get_toolset();
    if ($curr_toolset.len() == 0) {
        hefesto.project.abort(1);
    }
}

tulip-tests.prologue() {
    help();
    forge_libcutest();
    $ldflags = hefesto.sys.get_option("ldflags");
    if (hefesto.sys.os_name() == "linux") {
        $ldflags.add_item("-ldl");
        $ldflags.add_item("-lpthread");
    } else if (hefesto.sys.os_name() == "freebsd") {
        $ldflags.add_item("-lexecinfo");
        $ldflags.add_item("-lpthread");
    }
    $ldflags = get_master_project_objects($ldflags);
    $lib = hefesto.sys.get_option("libraries");
    $inc = hefesto.sys.get_option("includes");
    $cflags = hefesto.sys.get_option("cflags");
    $appname = get_appname();
    $src.ls(".*\\.c$");
}

tulip-tests.epilogue() {
    var exit_code type int;

    $exit_code = 1;

    if (hefesto.sys.last_forge_result() == 0) {
        $exit_code = runtests();
    }

    if ($exit_code != 0) {
        hefesto.sys.exit($exit_code);
    }
}

local function forge_libcutest() : result type none {
    var oldcwd type string;
    $oldcwd = hefesto.sys.pwd();
    hefesto.sys.cd("cutest/src");
    hefesto.sys.forge("cutest", "Forgefile.hsl", "--obj-output-dir=o --bin-output-dir=lib");
    hefesto.sys.cd($oldcwd);
    if (hefesto.sys.last_forge_result() != 0) {
        hefesto.project.abort(1);
    }
}

local function runtests() : result type int {
    var test_options type list;
    $test_options = hefesto.sys.get_option("test-options");

    if ($test_options.count() > 0) {
        var test_options_data type string;
        var t type int;
        $test_options_data = " ";
        $t = 0;
        while ($t < $test_options.count()) {
            $test_options_data = $test_options_data + $test_options.item($t) + " ";
            $t = $t + 1;
        }
    }

    var runline type string;
    $runline = hefesto.sys.make_path("bin", get_appname()) + $test_options_data;

    var valgrind_is_in_the_house type int;
    $valgrind_is_in_the_house = has_valgrind();

    var devels_forge type list;
    $devels_forge = hefesto.sys.get_option("devels-forge");

    if ($valgrind_is_in_the_house) {
        hefesto.sys.echo("\n" +
                         "*** NICE: You got Valgrind installed. What a nice place to run a build!\n" +
                         "          Let's run the tests using Valgrind (to inhibit it use --no-valgrind option).\n\n");
        var fuzz_nr type int;
        if ($devels_forge.count() == 0) {
            $fuzz_nr = 5000;
        } else {
            $fuzz_nr = 10;
        }
        $runline = wrap_cmdline_with_valgrind($runline + " --fuzz-nr=" + $fuzz_nr, "memcheck.log");
    }

    if ($devels_forge.count() == 0) {
        print_tester_monkey_banner();
        $runline = $runline + " --cutest-log-path=test.log";
    }

    var exit_code type int;
    $exit_code = hefesto.sys.run($runline);
    if ($valgrind_is_in_the_house) {
        var memcheck_lines type list;
        $memcheck_lines = hefesto.sys.lines_from_file("memcheck.log", "== All heap blocks were freed.*no leaks are possible");
        if ($memcheck_lines.count() == 0) {
            # INFO(Rafael): Sometimes still reachable blocks can show up, usually it is about some trinket lost in the
            #               Twilight Zone of libc, due to some implicit (mis)configuration. Well, under these conditions
            #               "still reachable" blocks are not user's fault.

            $memcheck_lines = hefesto.sys.lines_from_file("memcheck.log", "(==.*definitely lost: 0 bytes in 0 blocks.*|" +
                                                                          "==.*indirectly lost: 0 bytes in 0 blocks.*|" +
                                                                          "==.*possibly lost: 0 bytes in 0 blocks)");
            if ($memcheck_lines.count() > 0 && $memcheck_lines.count() != 3) {
                # INFO(Rafael): Better to explode.
                $memcheck_lines.clear();
            }
        }
        if ($memcheck_lines.count() == 0) {
            hefesto.sys.echo("\nWARNING: Issues were found by Valgrind...\n" +
                             "         Please, try to re-run the main forge under src sub-directory using the following command:\n\n" +
                             "                     hefesto --compile-model=debug\n\n" +
                             "         After it, please, open an issue on:\n\n" +
                             "                     https://github.com/rafael-santiago/tulip/issues\n\n" +
                             "         Attaching in this issue the file memcheck.log created under the sub-path src/test/\n" +
                             "Thank you!\n");
        } else {
            hefesto.sys.echo("\n*** INFO: All clean! No issues were found by Valgrind.\n");
        }
    }

    if ($devels_forge.count() == 0) {
        print_test_monkey_end_banner($exit_code);
    }

    result $exit_code;
}

local function get_appname() : result type string {
    if (hefesto.sys.os_name() == "windows") {
        result "tests.exe";
    }
    result "tests";
}

local function get_master_project_objects(ldflags type list) : result type list {
    var master_project_obj_dir type list;
    var m type int;
    var oldcwd type string;
    $master_project_obj_dir = hefesto.sys.get_option("master-project-obj-dir");
    if ($master_project_obj_dir.count() > 0) {
        $oldcwd = hefesto.sys.pwd();
        $m = 0;
        var objs type list;
        while ($m < $master_project_obj_dir.count()) {
            if (hefesto.sys.cd($master_project_obj_dir.item($m))) {
                $objs.ls(".*\\.o$");
                hefesto.sys.cd($oldcwd);
            }
            $m = $m + 1;
        }
        #
        # WARN(Santiago): Maybe some people like to use project's directory names with spaces.
        #
        $m = 0;
        while ($m < $objs.count()) {
            $ldflags.add_item("\"" + $objs.item($m) + "\"");
            $m = $m + 1;
        }
        $m = 0;
        while ($m < $ldflags.count()) {
            var citem type string;
            $citem = $ldflags.item($m);
            if ($citem.match(".*main\\.o\"$") == 1) {
                $ldflags.del_item($citem);
                break;
            }
            $m = $m + 1;
        }
    }
    result $ldflags;
}
